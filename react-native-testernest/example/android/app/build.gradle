plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'com.facebook.react'
}

react {
  entryFile = file('../index.js')
}

android {
  namespace 'com.testernest.example'
  compileSdkVersion rootProject.ext.compileSdkVersion

  defaultConfig {
    applicationId 'com.testernest.example'
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 1
    versionName '1.0'
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      signingConfig signingConfigs.debug
      minifyEnabled false
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = '17'
  }
}

dependencies {
  implementation 'com.facebook.react:react-android'
  if (hermesEnabled.toBoolean()) {
    implementation("com.facebook.react:hermes-android")
  } else {
    implementation 'org.webkit:android-jsc:+'
  }
  implementation project(':react-native-testernest')
  constraints {
    implementation("org.jetbrains.kotlin:kotlin-stdlib") {
      version { strictly("1.7.22") }
      because("RN 0.72 toolchain expects Kotlin 1.7.x")
    }
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7") {
      version { strictly("1.7.22") }
    }
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8") {
      version { strictly("1.7.22") }
    }
    implementation("org.jetbrains.kotlin:kotlin-reflect") {
      version { strictly("1.7.22") }
    }
  }
}
